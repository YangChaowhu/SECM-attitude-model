SUBROUTINE MCSNO( MJD, MJD0, CPRN, IPRN, BETA, MU, DINTV, &
                   XSV, SANTXYZ, VSVC, YANGLE, PHI)
!
!       NAME            MCSNO (version Dec  2022)
!
!     PURPOSE           PREDICT BDS-3 SECM SATELLITES YAW ANGLE
!                       (THE INPUT BODY-X UNIT VECTOR - SANTXYZ IS YAW-ROTATED
!                        BY PHI-YANGLE (THE ECL-NOMINAL) YAW ANGLE DIFFERENCE)
!
!       COPYRIGHT       CHAO YANG AND JING GUO, 2022.
!                       ALL RIGHTS RESERVED.
!                       ALL TERMS AND CONDITIONS APPLY AS DETAILED IN
!                       " TERMS AND CONDITIONS FOR SOFTWARE "
!
!       CONTACT         superyc@whu.edu.cn or jingguo@whu.edu.cn
!
!
!     PARAMETERS        DESCRIPTION
!
!        MJD            OBSERVATION EPOCH TIME (IN MODIFIED JULIAN DAY)
!        MJD0           START EPOCH TIME (IN MODIFIED JULIAN DAY)
!        CPRN           SV PRN 
!        IPRN           SV PRN NUMBER 
!        BETA           THE SUN ANGLE WITH ORBITAL PLANE(IN RAD)
!        MU             THE ORBITAL ANGLE (IN RAD)
!        DINTV          PROCESSING INTERVEL (IN SEC)
!        XSV(3)         SV X, Y, Z (m)(ITRF)
!        SANTXYZ        BODY-X UNIT VECTOR (ITRF)
!        VSVC           SV INERTIAL VELOCIRY VECTOR IN ITRF
!        YANGLE         THE NOMINAL YAW ANGLE
!        PHI            THE ECLIPSING YAW ANGLE
!
!
! *********************************************************************
!

IMPLICIT NONE

! THE CONSTANTS
!!-----------------------------
INTEGER*4, PARAMETER :: IT = SELECTED_INT_KIND(6)
INTEGER*4, PARAMETER :: RL = SELECTED_REAL_KIND(8,70)
INTEGER*4, PARAMETER :: LG = 4

REAL(RL), PARAMETER :: PI = 3.14159265358979323846D0
REAL(RL), PARAMETER :: RAD2DEG = 180.D0/PI
REAL(RL), PARAMETER :: DEG2RAD = PI/180.D0
INTEGER(IT), PARAMETER :: MAXSAT = 120


!*
! THE ARGUMENTS
!!-----------------------------

REAL(RL) MJD, MJD0
REAL(RL) MU
REAL(RL) XSV(*),SANTXYZ(*),VSVC(*)
REAL(RL) BETA, YANGLE, PHI, DINTV

CHARACTER(LEN=*) CPRN


  !*
  ! THE LOCAL VARIABLES
  !!-----------------------------
  REAL(RL) MURATE, BETADG, BETARATE(MAXSAT), MUDG, U0, LEFT, RIGHT
  REAL(RL) SANTX, SANTY, v(3), r(3)
  INTEGER(IT) J, IPRN, IYC, ASIGN, CSIGN, NCOUNT(MAXSAT)

  LOGICAL(LG) :: LFIRSTYC, LBEGIN(MAXSAT), LEND(MAXSAT)
  DATA LFIRSTYC, LBEGIN, LEND /.TRUE.,.FALSE.,.FALSE./
  REAL(RL) :: B_PRI(MAXSAT), T_PRI(MAXSAT), T_EPO(MAXSAT), T0(MAXSAT), TS(MAXSAT), TE(MAXSAT), NU0(MAXSAT), US(MAXSAT), UE(MAXSAT), PHI0(MAXSAT), PHIE(MAXSAT)
  SAVE B_PRI, T_PRI, T0, TS, TE, NU0, US, UE, PHI0, PHIE, LFIRSTYC, LBEGIN, LEND, NCOUNT, T_EPO, BETARATE

  !*
  ! START THE EXECTUABLE CODES
  !!-----------------------------


  IF (LFIRSTYC .EQ. .TRUE.) THEN
     DO IYC=1,MAXSAT
        B_PRI(IYC)=0.d0
        T_PRI(IYC)=MJD0
        T_EPO(IYC)=MJD0
        T0(IYC)=0.d0
        TS(IYC)=0.d0
        TE(IYC)=0.d0
        NU0(IYC)=0.d0
        US(IYC)=0.d0
        UE(IYC)=0.d0
        PHI0(IYC)=0.d0
        PHIE(IYC)=0.d0
        NCOUNT(IYC)=0
        BETARATE(IYC)=0
     END DO
     LFIRSTYC=.FALSE.
  END IF 

  IF (ABS((MJD-T_EPO(IPRN))*86400-DINTV) .LE. 0.1d0) THEN
     NCOUNT(IPRN)=0
  END IF

  ! THE ACTUAL SAT ORBIT ANGLE RATE (MURATE) (~0.00774 FOR BDS-3)
  MURATE=DSQRT((VSVC(1)**2+VSVC(2)**2+VSVC(3)**2)/(XSV(1)**2+XSV(2)**2+XSV(3)**2))*RAD2DEG

  BETADG=BETA*RAD2DEG
  MUDG=MU*RAD2DEG

  ! YAW ANLGE
  YANGLE=DACOS((SANTXYZ(1)*VSVC(1)+SANTXYZ(2)*VSVC(2)+SANTXYZ(3)*VSVC(3))/ &
                DSQRT(VSVC(1)**2+VSVC(2)**2+VSVC(3)**2))*RAD2DEG

  IF (BETADG .GT. 0.d0) YANGLE=-YANGLE

  IF (ABS(BETADG) .GT. 3.d0) THEN
    PHI=YANGLE
  ELSE
    IF(B_PRI(IPRN) .NE. 0.d0 .AND. NCOUNT(IPRN) .EQ. 0 ) THEN
       BETARATE(IPRN)=(BETADG-B_PRI(IPRN))/((MJD-T_PRI(IPRN))*86400.d0)
       IF((T0(IPRN) .EQ. 0.d0 .AND. 0.d0 .GE. (0.d0-BETADG)/BETARATE(IPRN) .AND. (0.d0-BETADG)/BETARATE(IPRN) .LT. DINTV) .OR. (T0(IPRN) .NE. 0.d0 .AND. T0(IPRN) .LT. MJD0)) THEN
         IF(LBEGIN(IPRN) .EQ. .FALSE.) THEN
           T0(IPRN)=MJD+(0.d0-BETADG)/BETARATE(IPRN)/86400.d0
           NU0(IPRN)=MUDG+(0.d0-BETADG)/BETARATE(IPRN)*MURATE
           PHI0(IPRN)=DATAN2(DTAN((SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)),DSIN(NU0(IPRN)*DEG2RAD))*RAD2DEG
           LBEGIN(IPRN)=.TRUE.
         END IF
       END IF
    ELSE IF (B_PRI(IPRN) .EQ. 0.d0) THEN
       IF ((MJD0 .GE. 58600.d0 .AND. MJD0 .LE. 58615.d0) .OR. (MJD0 .GE. 58948.d0 .AND. MJD0 .LE. 58963.d0)) THEN
          ASIGN=-1
       ELSE IF (MJD0 .GE. 58778.d0 .AND. MJD0 .LE. 58793.d0) THEN
          ASIGN=1
       END IF
       IF ((MJD0 .GE. 58634.d0 .AND. MJD0 .LE. 58645.d0) .OR. (MJD0 .GE. 58623.d0 .AND. MJD0 .LE. 58634.d0)) THEN
          CSIGN=1
       ELSE IF (MJD0 .GE. 58814.d0 .AND. MJD0 .LE. 58825.d0) THEN
          CSIGN=-1
       END IF

       SELECT CASE(TRIM(CPRN))
         CASE ('C25')
           BETARATE(IPRN)=0.000569/60.d0*CSIGN
         CASE ('C26')
           BETARATE(IPRN)=0.000601/60.d0*CSIGN
         CASE ('C27')
           BETARATE(IPRN)=0.000419/60.d0*ASIGN
         CASE ('C28')
           BETARATE(IPRN)=0.000425/60.d0*ASIGN
         CASE ('C29')
           BETARATE(IPRN)=0.000432/60.d0*ASIGN
         CASE ('C30')
           BETARATE(IPRN)=0.000425/60.d0*ASIGN
         CASE ('C34')
           BETARATE(IPRN)=0.000419/60.d0*ASIGN
         CASE ('C35')
           BETARATE(IPRN)=0.000410/60.d0*ASIGN
         CASE ('C43')
           BETARATE(IPRN)=0.000401/60.d0*ASIGN
         CASE ('C44')
           BETARATE(IPRN)=0.000392/60.d0*ASIGN
       END SELECT

       IF(BETARATE(IPRN) .NE. 0.d0) THEN
         IF((0.d0-BETADG)/BETARATE(IPRN) .LT. DINTV .AND. T0(IPRN) .EQ. 0.d0) THEN
           T0(IPRN)=MJD+(0.d0-BETADG)/BETARATE(IPRN)/86400.d0
           NU0(IPRN)=MUDG+(0.d0-BETADG)/BETARATE(IPRN)*MURATE
           PHI0(IPRN)=DATAN2(DTAN((SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)),DSIN(NU0(IPRN)*DEG2RAD))*RAD2DEG
         END IF
       END IF
    END IF

    IF(BETADG .GT. 0.d0) THEN
       PHI=DATAN2(DTAN(-3.d0*DEG2RAD),DSIN(MU))*RAD2DEG
    ELSE
       PHI=DATAN2(DTAN(3.d0*DEG2RAD),DSIN(MU))*RAD2DEG
    END IF

    IF (T0(IPRN) .GE. MJD0 .AND. MJD .GE. T0(IPRN)) THEN
       IF(ABS(PHI0(IPRN)) .LT. 5.d0 .AND. TS(IPRN) .EQ. 0.d0) THEN
         TS(IPRN)=T0(IPRN)
         US(IPRN)=NU0(IPRN)
         PHIE(IPRN)=PHI0(IPRN)
       ELSE
         PHI=DATAN2(DTAN((SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)),DSIN(MU))*RAD2DEG
         IF(ABS((MUDG-36.80)/MURATE) .LE. DINTV .AND. TS(IPRN) .EQ. 0.d0) THEN
           US(IPRN)=36.80
           TS(IPRN)=MJD+(36.80-MUDG)/MURATE/86400.d0
           PHIE(IPRN)=DATAN2(DTAN((SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)),DSIN(US(IPRN)*DEG2RAD))*RAD2DEG
         END IF
       END IF
    ELSE IF (T0(IPRN) .NE. 0.d0) THEN
       IF(ABS(PHI0(IPRN)) .LT. 5.d0 .AND. TS(IPRN) .EQ. 0.d0) THEN
         TS(IPRN)=T0(IPRN)
         US(IPRN)=NU0(IPRN)
         PHIE(IPRN)=PHI0(IPRN)
       ELSE
         IF(MUDG-NU0(IPRN) .LT. 360.d0 .AND. LEND(IPRN) .EQ. .FALSE.) THEN
            IF(MJD .GE. T0(IPRN) .AND. MUDG .LE. 36.80 .OR. NU0(IPRN) .GE. 36.80) THEN
               PHI=DATAN2(DTAN((SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)),DSIN(MU))*RAD2DEG
               IF(ABS((MUDG-36.80)/MURATE) .LE. DINTV .AND. TS(IPRN) .EQ. 0.d0) THEN
                 US(IPRN)=36.80
                 TS(IPRN)=MJD+(36.80-MUDG)/MURATE/86400.d0
                 PHIE(IPRN)=DATAN2(DTAN((SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)),DSIN(US(IPRN)*DEG2RAD))*RAD2DEG
               END IF
            ELSE
              LEND(IPRN)=.TRUE.
            END IF
         END IF
       END IF
    END IF

    IF(TS(IPRN) .NE. 0.d0) THEN
       U0=US(IPRN)+10.d0/0.055*MURATE
       LEFT=DATAN2(DTAN(SIGN(3.d0,BETARATE(IPRN))*DEG2RAD),DSIN(US(IPRN)*DEG2RAD))*RAD2DEG-DATAN2(-DTAN(SIGN(3.d0,BETARATE(IPRN))*DEG2RAD),DSIN(U0*DEG2RAD))*RAD2DEG+ &
            DTAN(SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)*DCOS(U0*DEG2RAD)*U0/(DSIN(U0*DEG2RAD)*DSIN(U0*DEG2RAD)+DTAN(SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)* &
            DTAN(SIGN(3.d0,BETARATE(IPRN))*DEG2RAD))+(SIGN(0.055,BETARATE(IPRN))*US(IPRN)/MURATE)
       RIGHT=SIGN(0.055,BETARATE(IPRN))/MURATE+DTAN(SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)*DCOS(U0*DEG2RAD)/(DSIN(U0*DEG2RAD)*DSIN(U0*DEG2RAD)+ &
             DTAN(SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)*DTAN(SIGN(3.d0,BETARATE(IPRN))*DEG2RAD))
       UE(IPRN)=LEFT/RIGHT
       TE(IPRN)=TS(IPRN)+(UE(IPRN)-US(IPRN))/MURATE/86400.d0
       IF(MJD .GE. TS(IPRN) .AND. MJD .LE. TE(IPRN)) THEN
          PHI=PHIE(IPRN)-SIGN(0.055,BETARATE(IPRN))*(MUDG-US(IPRN))/MURATE
       ELSE IF (MJD .GT. TE(IPRN)) THEN
          PHI=DATAN2(-DTAN((SIGN(3.d0,BETARATE(IPRN))*DEG2RAD)),DSIN(MU))*RAD2DEG
       END IF
    END IF
  END IF

  T_EPO(IPRN)=MJD
  NCOUNT(IPRN)=NCOUNT(IPRN)+1
  IF(NCOUNT(IPRN) .EQ. 1) THEN
     T_PRI(IPRN)=MJD
     B_PRI(IPRN)=BETADG
  END IF
  
  DO J=1,3
    V(J)=VSVC(J)/DSQRT(VSVC(1)**2+VSVC(2)**2+VSVC(3)**2)
    R(J)=XSV(J)/DSQRT(XSV(1)**2+XSV(2)**2+XSV(3)**2)
  END DO

  ! ROTATE X-VECTOR TO ECLIPSING YAW ANGLE PHI
  SANTX=(DCOS((PHI-YANGLE)*DEG2RAD)*(V(2)-V(3)*R(2)/R(3))-DCOS(PHI*DEG2RAD)* &
        (SANTXYZ(2)-SANTXYZ(3)*R(2)/R(3)))/(SANTXYZ(1)*V(2)-SANTXYZ(2)*v(1)+ &
        ((SANTXYZ(2)*V(3)-SANTXYZ(3)*V(2))*R(1)+(SANTXYZ(3)*V(1)- &
        SANTXYZ(1)*V(3))*R(2))/R(3))
  SANTY=(DCOS(PHI*DEG2RAD) - (V(1)-V(3)*R(1)/R(3))*SANTX)/(V(2)-V(3)*R(2)/R(3))
  ! THE BODY-X UNIT VECTOR ROTATED BY (PHI-YANGLE) RETURNED
  SANTXYZ(1)=SANTX
  SANTXYZ(2)=SANTY
  SANTXYZ(3)=(-R(1)*SANTX-R(2)*SANTY)/R(3)

1 RETURN


END SUBROUTINE
